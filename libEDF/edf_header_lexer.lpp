/**/
%top{
#include "edf_header_parser.h"

#ifndef YY_DECL
# define YY_DECL int yylex \
           (YYSTYPE * yylval_param, YYLTYPE * yylloc_param, EDF_File * edf_file)
#endif /* !YY_DECL */
}

DIGIT     [0-9]
LETTER    [a-zA-Z]
PUNCT     [\-!"#$%&'()*+,./:;<=>?@[\\\]^_`{|}~]
LETT_DIG  ({LETTER}|{DIGIT})
ID        {LETTER}{LETT_DIG}*
SPL_KEYS  [\-!#$%&'()*+,./:;<=>?@[\\\]^_`{|}~]
KEYBKEYS  ([ \t]|{LETT_DIG}|{SPL_KEYS})
EXT_ID    ({LETT_DIG}|{SPL_KEYS})

%option bison-bridge bison-locations noyywrap

%{
#define YY_USER_ACTION \
   do {\
      yylloc->first_column = yylloc->last_column;\
      yylloc->last_column += yyleng;\
      yylloc->bytesParsed += yyleng;\
   } while(0);
%}

%%

"}"\n {
            edf_file->getFields().headerLength = yylloc->bytesParsed;
            size_t totPixels = 1;
            for(size_t count = 0; count < edf_file->getData().dimensions.size();
                count++)
            {
              totPixels *= edf_file->getData().dimensions[count];
            }
            edf_file->getData().totPixels = totPixels;
            yyterminate();
          }

(";"|"=") {
            return *yytext;
          }

{DIGIT}+ {
            yylval->str.text = yytext;
            yylval->str.len = yyleng;
            return INTEGER;
          }

({DIGIT}+\.{DIGIT}*|\.{DIGIT}+) {
            yylval->str.text = yytext;
            yylval->str.len = yyleng;
            return FLOAT;
          }

Dim_{DIGIT}+ {
            yylval->num = atoi(yytext+4);
            return DIM;
          }

DataType {
            return DATA_TYPE;
          }

DoubleValue {
            return T_F64;
          }

(FloatValue|Float|Real) {
            return T_F32;
          }

Unsigned64 {
            return T_U64;
          }

Signed64 {
            return T_I64;
          }

(UnsignedInteger|UnsignedLong) {
            return T_U32;
          }

(SignedInteger|SignedLong) {
            return T_I32;
          }

UnsignedShort {
            return T_U16;
          }

SignedShort {
            return T_I16;
          }

UnsignedByte {
            return T_U08;
          }

SignedByte {
            return T_I08;
          }

{EXT_ID}+ {
            yylval->str.text = yytext;
            yylval->str.len = yyleng;
            return ID;
          }

"{"[ \t]*\n {
            yylloc->first_line++;
            yylloc->first_column = 1;
            yylloc->last_line++;
            yylloc->last_column = 1;
            yylloc->bytesParsed = yyleng;

            edf_file->getData().dimensions.resize(3, 1);
          }
\n {
            yylloc->first_line++;
            yylloc->first_column = 1;
            yylloc->last_line++;
            yylloc->last_column = 1;
          }

[ \t]+      /* eat up whitespace */

. {
            fprintf( stderr, "Line %4d Lexing Error, unknown symbol: '%s'\n",
                     yylloc->first_line, yytext );
            exit(1);
          }

<<EOF>> {
            fprintf( stderr, "Line %4d Lexing Error, unexpected EOF: '%s'\n",
                     yylloc->first_line, yytext );
            exit(1);
          }

%%

